#!/usr/bin/env perl

use strict;
use utf8;
use warnings;
use JSON;
use URI::Escape;
use Data::Dumper;

{

    package restclient;

    use Moo;
    with 'Role::REST::Client';
}

sub noEndSlash {
    my ($url) = @_;

    $url =~ s|/*$||;
    return $url;
}

my $dmdtaskdb = restclient->new(
    server      => noEndSlash( $ENV{'COUCHDB_DMDTASK'} ),
    type        => 'application/json',
    clientattrs => { timeout => 3600 },
);

$dmdtaskdb->set_persistent_header( 'Accept' => 'application/json' );
my $test = $dmdtaskdb->head("/");
if ( !$test || $test->code != 200 ) {
    die "Problem connecting to Couchdb database="
      . noEndSlash( $ENV{'COUCHDB_DMDTASK'} )
      . " Check configuration\n";
}

my $query = {
    "selector" => {
        '$and' => [
            {
                "fileName" => {
                    '$exists' => JSON::true
                }
            },
            {
                "progress" => {
                    '$exists' => JSON::false
                }
            }
        ]
    },
    "fields" => ["_id"],
    "limit"  => 1000
};

my $bookmark;
do {
    my $res =
      $dmdtaskdb->post( "/_find", $query,
        { deserializer => 'application/json' } );

    if ( $res->code != 200 ) {
        if ( defined $res->response->content ) {
            warn $res->response->content . "\n";
        }
        die "/_find returned " . $res->code . "\n";
    }

    if ( defined $res->data->{'warning'} ) {
        warn $res->data->{'warning'} . "\n";
    }

    # https://docs.couchdb.org/en/stable/api/database/find.html#pagination
    $bookmark = $res->data->{'bookmark'};
    undef $bookmark
      if ( ( scalar @{ $res->data->{docs} } < $query->{'limit'} )
        || ( defined $bookmark && ( $bookmark eq 'nil' ) ) );
    $query->{'bookmark'} = $bookmark;

    foreach my $row ( @{ $res->data->{docs} } ) {
        my $taskid = $row->{'_id'};

        #print Data::Dumper->Dump( [$row], [qw (task)] );

        my $url =
          "/_design/access/_update/canProcess/" . uri_escape_utf8($taskid);

        $dmdtaskdb->type("application/json");
        my $res = $dmdtaskdb->post(
            $url,
            { parse        => JSON::true },
            { deserializer => 'application/json' }
        );

        if ( $res->code != 201 && $res->code != 200 ) {
            if ( exists $res->{message} ) {
                warn( $taskid . ": message=" . $res->{message} . "\n" );
            }
            if ( exists $res->{content} ) {
                warn( $taskid . ": content=" . $res->{content} . "\n" );
            }
            if ( exists $res->{error} ) {
                warn( $taskid . ": error=" . $res->{error} . "\n" );
            }
            if (   ( ref $res->data eq "HASH" )
                && ( exists $res->data->{error} ) )
            {
                warn( $taskid . ": data error=" . $res->data->{error} . "\n" );
            }
            warn( $taskid . ": $url POST return code: " . $res->code . "\n" );
        }
    }

} until ( !$bookmark );
